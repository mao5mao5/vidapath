plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'org.liquibase.gradle' version '2.2.0'
    id 'checkstyle'
}

bootJar {
    archiveFileName = 'cytomine-app-engine.jar'
}

group = 'be.cytomine'
version = 'ce-0.0.0-SNAPSHOT' // do not modify this, it will be replace during build (ci)

java {
    sourceCompatibility = '17'
}

checkstyle {
    toolVersion = '10.21.2'
    configFile = file("${rootProject.projectDir}/checkstyle.xml")
}

configurations {
    liquibaseRuntime.extendsFrom runtimeClasspath
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://maven.dcm4che.org/"
    }
}

ext {
    mapstructVersion = "1.5.5.Final"
    lombokVersion = "1.18.30"
    lombokMapstructBindingVersion = "0.2.0"
}

springBoot {
    mainClass = "be.cytomine.appengine.AppEngineApplication"
}
test {
    filter {
        excludeTestsMatching "be.cytomine.appengine.integration.*"
    }
}
dependencies {
    // Lombok
    implementation "org.mapstruct:mapstruct:${mapstructVersion}", "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}", "org.projectlombok:lombok:${lombokVersion}", "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation ('org.springframework.boot:spring-boot-starter-web') {
        // https://github.com/networknt/json-schema-validator/issues/1207
        // https://spring.io/blog/2025/10/07/introducing-jackson-3-support-in-spring#keep-using-jackson-2-support-temporarily
        exclude group:'org.springframework.boot', module:'spring-boot-jackson'
    }
    implementation 'org.springframework.boot:spring-boot-jackson2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    compileOnly("org.springframework.boot:spring-boot-devtools")

    implementation group: 'com.networknt', name: 'json-schema-validator', version: '1.0.87'
    implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.24.0'
    implementation group: 'org.apache.tika', name: 'tika-core', version: '2.9.1'
    implementation 'org.apache.commons:commons-fileupload2-jakarta-servlet6:2.0.0-M4'
    implementation 'commons-io:commons-io:2.15.1'
    // Geometry
    implementation 'org.locationtech.jts:jts-core:1.19.0'
    implementation 'org.locationtech.jts.io:jts-io-common:1.19.0'

    // Hibernate
    implementation 'org.hibernate:hibernate-core:7.1.8.Final'
    implementation 'org.hibernate:hibernate-spatial:7.1.8.Final'


    // Jakarta
    implementation 'jakarta.persistence:jakarta.persistence-api'
    implementation 'jakarta.servlet:jakarta.servlet-api'

    // PostgreSQL
    runtimeOnly 'org.postgresql:postgresql'

    // Kubernetes
    implementation 'io.fabric8:kubernetes-client:6.10.0'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.59'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.59'

    // Test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    // Cucumber dependencies
    implementation group: 'io.cucumber', name: 'cucumber-spring', version: '6.10.4'
    testImplementation 'io.cucumber:cucumber-java:6.10.4'
    testImplementation 'io.cucumber:cucumber-junit:6.10.4'
    testImplementation 'org.assertj:assertj-core:3.4.1'
    testImplementation 'org.junit.vintage:junit-vintage-engine'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation "org.junit.jupiter:junit-jupiter"

    // registry client
    implementation project(':registry-client')

    // liquibase
    liquibaseRuntime 'info.picocli:picocli:4.7.5'
    implementation 'org.liquibase:liquibase-core:4.25.0'

    // Image processing
    implementation 'org.apache.commons:commons-imaging:1.0.0-alpha5'
    implementation 'org.dcm4che:dcm4che-core:5.31.0'

    testImplementation "org.springframework.boot:spring-boot-testcontainers"
    testImplementation "org.testcontainers:testcontainers-junit-jupiter"
    testImplementation 'com.google.cloud.tools:jib-core:0.27.3'
    testImplementation "org.testcontainers:testcontainers-postgresql"
    testImplementation "org.testcontainers:testcontainers"
    testImplementation "org.testcontainers:testcontainers-k3s"

}

tasks.named('test') {
    useJUnitPlatform()
}

// Jacoco coverage report is always generated after tests run
test.finalizedBy jacocoTestReport

jacocoTestReport {
}

// Cucumber as a test implementation
configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

// run cucumber to verify features
tasks.register('cucumber') {
    group "verification"
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'be.cytomine.appengine', 'src/test/resources']
        }
    }
}
